/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.goooler.shadow' version "8.1.7"
}

ext {
    opensearch_version = System.getProperty("opensearch.version", "3.0.0-alpha1-SNAPSHOT")
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
    buildVersionQualifier = System.getProperty("build.version_qualifier", "alpha1")

    // 2.0.0-rc1-SNAPSHOT -> 2.0.0.0-rc1-SNAPSHOT
    version_tokens = opensearch_version.tokenize('-')
    opensearch_build = version_tokens[0] + '.0'

    if (buildVersionQualifier) {
        opensearch_build += "-${buildVersionQualifier}"
    }
    if (isSnapshot) {
        opensearch_build += "-SNAPSHOT"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
    compileOnly project(path: ":opensearch-resource-sharing-spi")
    compileOnly project(":")
}

shadowJar {
    archiveClassifier.set(null)
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from tasks.javadoc
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
            pom {
                name.set("OpenSearch Security Client")
                packaging = "jar"
                description.set("OpenSearch Security Client")
                url.set("https://github.com/opensearch-project/security")
                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                scm {
                    connection.set("scm:git@github.com:opensearch-project/security.git")
                    developerConnection.set("scm:git@github.com:opensearch-project/security.git")
                    url.set("https://github.com/opensearch-project/security.git")
                }
                developers {
                    developer {
                        name.set("OpenSearch Contributors")
                        url.set("https://github.com/opensearch-project")
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Snapshots"
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
        maven {
            name = 'staging'
            url = "${rootProject.buildDir}/local-staging-repo"
        }
    }
}
