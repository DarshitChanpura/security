/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java'
    id 'maven-publish'
}

ext {
    opensearch_version = System.getProperty("opensearch.version", "3.0.0-SNAPSHOT")
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from tasks.javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name.set("OpenSearch Resource Sharing SPI")
                description.set("OpenSearch Security Resource Sharing")
                url.set("https://github.com/opensearch-project/security")
                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                scm {
                    connection.set("scm:git@github.com:opensearch-project/security.git")
                    developerConnection.set("scm:git@github.com:opensearch-project/security.git")
                    url.set("https://github.com/opensearch-project/security.git")
                }
                developers {
                    developer {
                        name.set("OpenSearch Contributors")
                        url.set("https://github.com/opensearch-project")
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Snapshots"
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
}
